;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; POST: The result is in the range [0,BLS12_381_R)
;;
;; subFrBLS12381:
;;             in: A,C ∈ [0, 2²⁵⁶-1]
;;             out: C = A - C (mod BLS12_381_R) ∈ Fr
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

subFrBLS12381:
        ; 1] Compute and check the sub over Z
        ; A·[1] + [BLS12_381_R-C] = [D]·2²⁵⁶ + [E]
        1 => B
        ${const.BLS12_381_R - C} => C
        $${var _subFrBLS12381_AC = A + C}
        ${_subFrBLS12381_AC >> 256} => D
        ${_subFrBLS12381_AC & ((1 << 256) - 1)} => E :ARITH

        ; 2] Check it over Fr, that is, it must be satisfied that:
        ; [BLS12_381_R]·[(A - C) / r] + [(A - C) % r] = D·2²⁵⁶ + E
        ; where C < BLS12_381_R
        %BLS12_381_R => A
        ${_subFrBLS12381_AC / const.BLS12_381_R} => B        ; quotient
        ${_subFrBLS12381_AC % const.BLS12_381_R} => C        ; residue
        E :ARITH

        ; 3] Check that the residue is lower than BLS12_381_R
        A => B
        C => A
        1       :LT, RETURN