;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; POST: The result is in the range [0,BLS12_381_R)
;;
;; squareFrBLS12381:
;;             in: A ∈ [0, 2²⁵⁶-1]
;;             out: A = A² (mod BLS12_381_R) ∈ Fr
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

squareFrBLS12381:
        ; 1] Compute and check the inverse over Z
        ; A·A + [0] = [D]·2²⁵⁶ + [E]
        A => B
        0 => C
        $${var _squareFrBLS12381_AA = A * A}
        ${_squareFrBLS12381_AA >> 256} => D
        ${_squareFrBLS12381_AA & ((1 << 256) - 1)} => E :ARITH

        ; 2] Check it over Fr, that is, it must be satisfied that:
        ; [BLS12_381_R]·[A² / r] + [A² % r] = D·2²⁵⁶ + E
        ; where C < BLS12_381_R
        %BLS12_381_R => A
        ${_squareFrBLS12381_AA / const.BLS12_381_R} => B        ; quotient
        ${_squareFrBLS12381_AA % const.BLS12_381_R} => C        ; residue
        E :ARITH

        ; 3] Check that the residue is lower than BLS12_381_R
        A => B
        C => A
        1       :LT, RETURN